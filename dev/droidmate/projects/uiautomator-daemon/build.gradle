// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org

import org.droidmate.buildsrc.BuildKt

def libsDir = file("$projectDir/libs")
evaluationDependsOn(":projects:uiautomator-daemon-lib")
def projectUiautomatorDaemonLib = findProject(":projects:uiautomator-daemon-lib")
Jar uiautomatorDaemonLibJar = projectUiautomatorDaemonLib.jar
ConfigurableFileTree libsDirFileTree = fileTree(dir: libsDir, include: '*.jar', excludes: ['*javadoc.jar', '*sources.jar', uiautomatorDaemonLibJar.archiveName])
/* Making this additional setter call instead of supplying a value in constructor is a workaround for an issue I discovered
  on Nov 23, 2014 in Gradle 2.1. See OneNote "Gradle bugs". */
libsDirFileTree.setBuiltBy(['pullLibCommonAndroidJar'])

/* --- MAKE INTELLIJ IDEA HAPPY ZONE ---

  The code below is here to make sure that when "Refresh all Gradle projects" is hit in IntelliJ IDEA, the source folders
  and dependencies will be properly configured in the IDE.

  This is necessary because this project isn't actually a java project, but it is a wrapper over an ant script:
  see the 'build' task.
*/

apply plugin: 'java'
apply from: project(":projects").file("debug.gradle")


/* The tasks below are pruned and/or disabled as they come from Gradle's java plugin which is applied
  only to make IntelliJ happy and is not fully configured.

  There are more tasks like that that should be disabled, but I hope you know what you are doing.

  Use only "build" and "clean" tasks. */
tasks.build.dependsOn.remove("check")
tasks.build.dependsOn.remove("assemble")
tasks.check.enabled = false
tasks.assemble.enabled = false
tasks.jar.enabled = false

/* "antBuild" and "antClean" tasks are defined below */
tasks.build.dependsOn("antBuild")
tasks.clean.dependsOn("cleanLibCommonAndroidJar", "cleanAntOutput")

buildDir = "bin"

sourceSets {
  main.java.srcDirs = ["src"]
  test.java.srcDirs = []
  main.resources.srcDirs = []
  test.resources.srcDirs = []
  main.output.classesDir = buildDir
  test.output.classesDir = buildDir
}

dependencies {
  compile fileTree(dir: "src")

  compile libsDirFileTree
  compile projectUiautomatorDaemonLib

  // Required by IntelliJ; when building with Gradle, the underlying ant script knows this dependency.
  compileOnly files(BuildKt.uiautomator_jar_api19.toString())

  compileOnly files(BuildKt.android_jar_api19.toString())
}

/* --- end of MAKE INTELLIJ IDEA HAPPY ZONE --- */

task pullLibCommonAndroidJar(type: Copy, dependsOn: ":projects:uiautomator-daemon-lib:jar") { Copy copy ->
  
  copy.from(uiautomatorDaemonLibJar.archivePath).into(libsDir)
}

task cleanLibCommonAndroidJar(type: Delete) {
  group = "build"
  delete new File(libsDir, uiautomatorDaemonLibJar.archiveName)
}

def antBuildFileName = "build-customized.xml"

/*
  Note: the underlying ant script inputs all files in libs dir, so if some additional .jar files will be
  found in it (e.g. with sources), the 'antBuild' task will fail to produce output due to classpath clashes.
 */
task antBuild(dependsOn: "pullLibCommonAndroidJar") {
  group = "build"
  description = "Runs the underlying customized ant build file with 'ant build'. The ant file was originally " +
    "generated with Android SDK."

  inputs.files configurations.compile, new File(libsDirFileTree.dir, uiautomatorDaemonLibJar.archiveName)
  outputs.dir buildDir

  doLast {
    int exitValue = BuildKt.executeCommand(
      "${project.name} ant build",
      "ant -f $projectDir/$antBuildFileName build"
    )
    if (exitValue != 0)
      throw new GradleException("Failed to build uiautomator-daemon.jar (ant script return code != 0)")
  }
}

task cleanAntOutput(type: Delete) { group = "build"; delete buildDir }