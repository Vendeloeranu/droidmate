import com.konradjamrozik.IterableExtensions
import org.droidmate.buildsrc.BuildKt

// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org
/*
  This project contains classes reused by multiple projects, none of which are run on an Android device.
  Because none of them are run on an Android Device, the classes in this project can and are compiled with Groovy and Java 8.
*/

apply plugin: 'groovy'
apply plugin: 'project-report'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

File resDir = IterableExtensions.findSingle(sourceSets.main.resources.srcDirs)

dependencies {

  // "gradlew build" works without this line, but IntelliJ "make" is unhappy without it. Looks like IntelliJ needs it to pick-up
  // the META-INF/services/org.codehaus.groovy.runtime.ExtensionModule file.
  runtime files(new File(resDir, "."))

  compile "com.github.konrad-jamrozik:utilities:$utilities_version"

  // Groovy lang
  compile 'org.codehaus.groovy:groovy-all:2.4.6'

  // General utils
  compile 'org.apache.commons:commons-exec:1.2'
  compile 'org.apache.commons:commons-lang3:3.3'
  compile 'commons-io:commons-io:2.4'

  // Command line parsing
  compile 'net.sf.jopt-simple:jopt-simple:4.9'

  // Logging
  compile 'ch.qos.logback:logback-classic:1.0.13'
  compile 'ch.qos.logback:logback-core:1.0.13'
  compile 'org.slf4j:slf4j-api:1.7.6'

  // Override guava 16 used by jimfs (added below)
  compile 'com.google.guava:guava:19.0'

  // Filesystem stubbing
  compile 'com.google.jimfs:jimfs:1.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'
}

File propertiesFile = new File(resDir, "buildConstants.properties")

task writeBuildConstantsProperties() { Task it ->
  
  it.outputs.file(propertiesFile)
  
  doLast {
    Properties properties = new Properties()
    // KJA populate with all BuildKt props that are used inside the built/compiled classes. Then: rewire code.
    properties.setProperty("apk_inliner_param_input", BuildKt.apk_inliner_param_input)
    properties.setProperty("apk_inliner_param_output_dir", BuildKt.apk_inliner_param_output_dir)
    properties.store(new FileOutputStream(propertiesFile), null)
  }
}

clean.configure { doLast { file(propertiesFile).delete() } }

processResources.dependsOn("writeBuildConstantsProperties")

apply from: project(":projects").file("maven.gradle")