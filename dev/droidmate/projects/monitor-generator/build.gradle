// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org

import org.droidmate.buildsrc.BuildKt

apply plugin: 'groovy'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// For explanations of these commands, please see :command project build.gradle
configurations {forceJunitPriority}
sourceSets.test.runtimeClasspath = configurations.forceJunitPriority + sourceSets.test.runtimeClasspath
sourceSets.test.compileClasspath = configurations.forceJunitPriority + sourceSets.test.compileClasspath

dependencies {

  compile project(":projects:lib-common")

  compile 'org.codehaus.groovy:groovy-all:2.4.6'

  forceJunitPriority 'junit:junit:4.12'
  testCompile 'junit:junit:4.12'

  compile project(":projects:monitor-template")
}

evaluationDependsOn(":projects:monitor-template")
// !!! DUPLICATION WARNING !!! with the file name and file location of class org.droidmate.monitor_template_src.MonitorJavaTemplate
File monitorJavaTemplateSource = findProject(":projects:monitor-template").sourceSets.main.java.find { it.name == "MonitorJavaTemplate.java" } as File
assert monitorJavaTemplateSource.file

SourceDirectorySet resDirs = sourceSets.main.resources
assert resDirs.srcDirs.size() == 1
File resDir = resDirs.srcDirs[0]

apply plugin: 'application'
mainClassName = "org.droidmate.monitor_generator.MonitorGeneratorFrontend"

processResources.dependsOn "pullMonitorJavaTemplateSource"

task pullMonitorJavaTemplateSource(type: Copy) { Copy copy ->

  copy.from monitorJavaTemplateSource 
  copy.into resDir
  copy.rename { BuildKt.monitor_generator_res_name_monitor_template }
}

JavaExec runTask = tasks.run as JavaExec
File generatedMonitor_api19 = new File(runTask.workingDir, BuildKt.monitor_generator_output_relative_path_api19)
File generatedMonitor_api23 = new File(runTask.workingDir, BuildKt.monitor_generator_output_relative_path_api23)
def generateMonitor = { JavaExec je, List<String> args, File generatedMonitor ->
  je.group = "build"
  ["main", "classpath", "standardOutput"].each {je."$it" = runTask."$it"}

  je.args = args
  je.standardOutput = new ByteArrayOutputStream() // Mute the task stdout
  je.outputs.file generatedMonitor

  je.doLast { assert generatedMonitor.file }
}

task generateMonitor_api19(type: JavaExec) {JavaExec je -> je.group = "build" ; generateMonitor(je, ["api19"], generatedMonitor_api19) }
task generateMonitor_api23(type: JavaExec) {JavaExec je -> je.group = "build" ; generateMonitor(je, ["api23"], generatedMonitor_api23) }

File monitorApkScaffoldingDir = file "monitor-apk-scaffolding"
assert monitorApkScaffoldingDir.directory

File libsDir = new File(monitorApkScaffoldingDir, "libs")
File libs_api19_dir = new File(monitorApkScaffoldingDir, "libs_api19")
File libs_api23_dir = new File(monitorApkScaffoldingDir, "libs_api23")

task cleanLibsDir(type: Delete) {group = "build"; delete libsDir }
task cleanLibsDir_api19(dependsOn: "cleanLibsDir") { group = "build" }
task cleanLibsDir_api23(dependsOn: "cleanLibsDir") { group = "build" }

task make_libs_from_libs_api19(type: Copy, dependsOn: "cleanLibsDir_api19") { Copy copy ->
  group = "build"
  copy.from(libs_api19_dir).into(libsDir)
}

task make_libs_from_libs_api23(type: Copy, dependsOn: "cleanLibsDir_api23") { Copy copy ->
  group = "build"
  copy.from(libs_api23_dir).into(libsDir)
}

File monitorJava = new File(monitorApkScaffoldingDir, "src/org/droidmate/monitor_generator/generated/Monitor.java")
assert monitorJava.parentFile.directory

def copyGeneratedMonitorToScaffolding = {Copy copy, File copyInput  ->
  copy.group = "build"

  copy.from copyInput
  copy.into monitorJava.parent
  copy.rename copyInput.name, monitorJava.name

  copy.outputs.file monitorJava

  copy.doLast {assert monitorJava.file}
}

task copyGeneratedMonitorToScaffolding_api19(type: Copy, dependsOn: "generateMonitor_api19") {Copy copy ->
  copyGeneratedMonitorToScaffolding(copy, generatedMonitor_api19)
}

task copyGeneratedMonitorToScaffolding_api23(type: Copy, dependsOn: "generateMonitor_api23") {Copy copy ->
  copyGeneratedMonitorToScaffolding(copy, generatedMonitor_api23)
}

def pullLibCommonJar = {Copy copy ->
  group = "build"
  copy.from(findProject(":projects:lib-common").jar.archivePath).into(libsDir)
}
task pullLibCommonJar_api19(type: Copy, dependsOn: [":projects:lib-common:jar", "make_libs_from_libs_api19"]) {Copy copy -> pullLibCommonJar(copy) }
task pullLibCommonJar_api23(type: Copy, dependsOn: [":projects:lib-common:jar", "make_libs_from_libs_api23"]) {Copy copy -> pullLibCommonJar(copy) }

File antOutput = file("${monitorApkScaffoldingDir.path}/bin")
File monitorApkOutputByAnt = file(new File(antOutput,"monitor-apk-debug.apk"))
def antBuildMonitorApk = { Task task ->
  task.group = "build"

  task.inputs.files monitorJava
  task.inputs.files fileTree(dir: "${monitorApkScaffoldingDir.path}/libs", include: '**')
  task.outputs.files monitorApkOutputByAnt
  task.outputs.dir buildDir

  task.doLast {

    assert monitorJava.file

    int exitValue = BuildKt.executeCommand(
      "${monitorApkScaffoldingDir.name} ant debug",
      "ant -f ${monitorApkScaffoldingDir.path}/build.xml debug"
    )
    if (exitValue != 0)
      throw new GradleException("Failed to build ${monitorApkScaffoldingDir.name}.")
  }
}

task antBuildMonitorApk_api19(dependsOn: ["make_libs_from_libs_api19", "copyGeneratedMonitorToScaffolding_api19", "pullLibCommonJar_api19"]) { Task task -> antBuildMonitorApk(task) }
task antBuildMonitorApk_api23(dependsOn: ["make_libs_from_libs_api23", "copyGeneratedMonitorToScaffolding_api23", "pullLibCommonJar_api23"]) { Task task -> antBuildMonitorApk(task) }

task buildMonitorApk_api19(dependsOn: ["antBuildMonitorApk_api19"]) { group = "build" }
tasks.cleanLibsDir_api23.mustRunAfter(tasks.buildMonitorApk_api19)
task buildMonitorApk_api23(dependsOn: ["antBuildMonitorApk_api23"]) { group = "build" }


task deployMonitorApk_api19(type: Copy, dependsOn: "buildMonitorApk_api19") {Copy copy ->
  group = "build"
  copy.from(monitorApkOutputByAnt).into(buildDir).rename {"monitor_api19.apk"}
}

task deployMonitorApk_api23(type: Copy, dependsOn: "buildMonitorApk_api23") {Copy copy ->
  group = "build"
  copy.from(monitorApkOutputByAnt).into(buildDir).rename {"monitor_api23.apk"}
}

task cleanAntOutput(type: Delete) { group = "build"; delete antOutput }

task cleanRun(type: Delete) {
  group = "build"
  delete generatedMonitor_api19
  delete generatedMonitor_api23
  assert generatedMonitor_api19.parent == generatedMonitor_api23.parent
  delete generatedMonitor_api19.parent
  delete monitorJava
}

test.configure {

  // Workaround for GRADLE-1682. See http://stackoverflow.com/a/14947906/986533
  // includes = ['**/MonitorGeneratorFrontendTest.class']
  // Test not included because the test shouldn't be run as part of a test suite.
  includes = ['']

}

clean.dependsOn "cleanRun", "cleanAntOutput", "cleanLibsDir"
build.dependsOn "deployMonitorApk_api19", "deployMonitorApk_api23"

// Ensure BuildKt.appguard_apis_txt is available at runtime as a resource. 
processResources.configure { Copy copy -> copy.from(project(":projects").file("resources")) }

apply from: project(":projects").file("debug.gradle")
