// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org
/*
  This project contains classes:
  1. which require access to Android API: they depend on Android.jar;
  2. whose source will be deployed to Android device, which requires Java 7.
*/

import org.droidmate.buildsrc.BuildKt

apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7
compileJava.configure { JavaCompile jc ->
  /*
    Suppresses the following warning:
    
      warning: [options] bootstrap class path not set in conjunction with -source 1.7
      
    We do not worry about this as the code will be deployed to Android device which should provide appropriate bootstrap class.
   */
  jc.options.compilerArgs += "-Xlint:-options"
}

dependencies {

  testCompile 'junit:junit:4.12'

  compileOnly files(BuildKt.android_jar_api19.toString())

  compile project(":projects:lib-common")
}

test.configure {

  /* The tests have to be run from root project dir (droidmate) for the classpaths to be properly resolved.

   API reference:
   workingDir  http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html
   rootDir     http://www.gradle.org/docs/current/dsl/org.gradle.api.Project.html
   */
  workingDir rootDir

  /* Lurking bug: GRADLE-1682. */
  includes = ['**/MonitorJavaTemplateTest.class']

}

apply from: project(":projects").file("maven.gradle")