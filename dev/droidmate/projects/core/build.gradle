// DroidMate, an automated execution generator for Android apps.
// Copyright (C) 2012-2016 Konrad Jamrozik
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.
//
// email: jamrozik@st.cs.uni-saarland.de
// web: www.droidmate.org
apply plugin: 'groovy'
apply from: project(":projects").file("debug.gradle")

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// See the comment to "evaluationDependsOn" in 'command' project for explanation of these dependencies.
evaluationDependsOn(":projects:lib-common")

// See explanation for equivalent lines in ":projects:command:build.gradle"
configurations {forceJunitPriority}
sourceSets.test.runtimeClasspath = configurations.forceJunitPriority + sourceSets.test.runtimeClasspath
sourceSets.test.compileClasspath = configurations.forceJunitPriority + sourceSets.test.compileClasspath

dependencies {

  compile("com.github.konrad-jamrozik:utilities:$utilities_version") {
    exclude group: "org.slf4j", module: "slf4j-simple"
  }

  forceJunitPriority 'junit:junit:4.12'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-core:1.3'

  compile project(":projects:lib-common")
  compile project(":projects:lib-kotlin")
  compile project(":projects:uiautomator-daemon-lib")

  compile 'com.beust:jcommander:1.35'
  compile 'com.google.guava:guava:19.0'
  compile 'org.apache.commons:commons-lang3:3.3'
  compile 'commons-io:commons-io:2.4'
  compile 'org.apache.commons:commons-exec:1.2'
  compile 'org.codehaus.groovy:groovy-all:2.4.6'
  compile 'org.slf4j:slf4j-api:1.7.6'
  compile 'ch.qos.logback:logback-classic:1.0.13'
  compile 'ch.qos.logback:logback-core:1.0.13'
  compile 'net.sf.opencsv:opencsv:2.3'

  // For visualizing device GUI model graphs
  compile 'org.graphstream:gs-core:1.3'

  // Filesystem stubbing
  compile 'com.google.jimfs:jimfs:1.0'

  // Only for org.droidmate.tests_misc.JavaReprosTest.BBoxError
  testCompile 'de.erichseifert.vectorgraphics2d:VectorGraphics2D:0.10'

  // See the comment to "evaluationDependsOn" in 'command' project for explanation of these dependencies.
  testCompile project(":projects:lib-common").sourceSets.test.output

}

apply from: file("resources.gradle")

test.configure {

  inputs.dir apkFixturesDir.absolutePath

  /* The tests have to be run from root project dir (droidmate) for the classpaths to be properly resolved.

  API reference:
  workingDir  http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html
  rootDir     http://www.gradle.org/docs/current/dsl/org.gradle.api.Project.html
  */
  workingDir rootDir

   /* Lurking bug: GRADLE-1682. See :projects:command:test doc for more.*/
   includes = ['']
}

apply from: project(":projects").file("maven.gradle")

apply from: project(":projects").file("testsConfig.gradle")

/* WISH right now logback.groovy is in the built jar, thus making it hard to replace:
 http://stackoverflow.com/a/18275945/986533
 http://stackoverflow.com/a/26452714/986533

 More precisely, because it is in resources dir of main sourceSet, it gets deployed to the jar with :jar task. Ideally it
 should not be in the jar at all, nor in the distribution ('application' plugin / 'installDist' task). See the links above
 on the better setup.
*/