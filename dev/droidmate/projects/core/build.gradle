// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org

import com.konradjamrozik.IterableExtensions
import org.droidmate.buildsrc.BuildKt

apply plugin: 'groovy'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


//region Dependencies

// See explanation for equivalent lines in ":projects:command:build.gradle"
configurations {forceJunitPriority}
sourceSets.test.runtimeClasspath = configurations.forceJunitPriority + sourceSets.test.runtimeClasspath
sourceSets.test.compileClasspath = configurations.forceJunitPriority + sourceSets.test.compileClasspath

dependencies {

  compile("com.github.konrad-jamrozik:utilities:$utilities_version") {
    exclude group: "org.slf4j", module: "slf4j-simple"
  }

  forceJunitPriority 'junit:junit:4.12'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-core:1.3'

  compile project(":projects:lib-common")
  compile project(":projects:lib-common-kotlin")
  compile project(":projects:uiautomator-daemon-lib")

  compile 'com.beust:jcommander:1.35'
  compile 'com.google.guava:guava:19.0'
  compile 'org.apache.commons:commons-lang3:3.3'
  compile 'commons-io:commons-io:2.4'
  compile 'org.apache.commons:commons-exec:1.2'
  compile 'org.codehaus.groovy:groovy-all:2.4.6'
  compile 'org.slf4j:slf4j-api:1.7.6'
  compile 'ch.qos.logback:logback-classic:1.0.13'
  compile 'ch.qos.logback:logback-core:1.0.13'
  compile 'net.sf.opencsv:opencsv:2.3'

  // For visualizing device GUI model graphs
  compile 'org.graphstream:gs-core:1.3'

  // Filesystem stubbing
  compile 'com.google.jimfs:jimfs:1.0'

}


/* Note: The 'compile[Test]Java.dependsOn' clauses are redundant with 'builtBy' property in the 'dependencies' closure,
   but they are left here for documentation purposes.
 */
processResources.dependsOn "pullMonitorApk", "pullUiaDaemonApk", "pullUiaDaemonTestApk"

/*
  This dependency ensures that on "gradlew clean build" all the apk fixtures will be rebuilt as well as the library .jar they
  depend on.
 */
clean.dependsOn "cleanTest", "cleanResources", "cleanUiautomator2Daemon"

processTestResources.dependsOn "pullApkFixtures", "inlineMonitoredApkFixture"
//endregion Dependencies

// KJA modularize, switch to android v4 (uia-1, not uia-2).
//region Preparation of resources
String uiaDaemonName = BuildKt.uia2_daemon_project_name
File uiaDaemonProjectDir = new File(rootDir, BuildKt.uia2_daemon_relative_project_dir.path)
assert uiaDaemonProjectDir.isDirectory()
File uiaDaemonBuildDir = new File("$uiaDaemonProjectDir/build")

String uiaDaemonApkName = "${uiaDaemonName}.apk"
String uiaDaemonTestApkName = "${uiaDaemonName}-test.apk"
String uiaDaemonApkOriginalName = "${uiaDaemonName}-debug-unaligned.apk"
String uiaDaemonTestApkOriginalName = "${uiaDaemonName}-debug-androidTest-unaligned.apk"
String monitorApkName = "monitor.apk"

task assembleUiautomator2Daemon(type: GradleBuild) { GradleBuild task ->
  task.buildFile = new File(uiaDaemonProjectDir, "build.gradle")
  task.tasks = ['assemble', 'assembleAndroidTest']
}

task cleanUiautomator2Daemon(type: GradleBuild) { GradleBuild task ->
  task.buildFile = new File(uiaDaemonProjectDir, "build.gradle")
  task.tasks = ['clean']
}

task pullUiaDaemonApk(type: Copy, dependsOn: "assembleUiautomator2Daemon") {
  Copy copy ->
    def uiaDaemonApk = file(new File(uiaDaemonBuildDir.absolutePath + "/outputs/apk/", uiaDaemonApkOriginalName))
    copy.from(uiaDaemonApk).into(coreResDir).rename("-debug-unaligned.apk", ".apk")
}

task pullUiaDaemonTestApk(type: Copy, dependsOn: "assembleUiautomator2Daemon") {
  Copy copy ->
    def uiaDaemonTestApk = file(new File(uiaDaemonBuildDir.absolutePath + "/outputs/apk/", uiaDaemonTestApkOriginalName))
    copy.from(uiaDaemonTestApk).into(coreResDir).rename("debug-androidTest-unaligned.apk", "test.apk")
}

Project monitorGenerator = findProject(":projects:monitor-generator")
task pullMonitorApk(type: Copy, dependsOn: ":projects:monitor-generator:build") {Copy copy ->

  def monitorApk = file(new File(monitorGenerator.buildDir, monitorApkName))
  copy.from(monitorApk).into(coreResDir)
}

// endregion Preparation of resources for the 'compileGroovy' task

//region Preparation of resources for the 'test' task: apk fixtures

// !!! DUPLICATION WARNING !!!
// These values have to be the same as the ones used in the gradle scripts of the referenced "apk fixtures" project.
File apkFixturesProjectDir = file("${project.rootDir}/../apk_fixtures_src")
assert apkFixturesProjectDir.directory
File apkFixturesDroidmateStagingDir = new File("${apkFixturesProjectDir.path}/build")
String apkFixturesBuildTask = "stageForDroidmate"
String monitoredApkFixtureName = "MonitoredApkFixture-debug.apk"
// end of DUPLICATION WARNING

/*
 This project sometimes has to fire apk-inliner:run. For that, this project needs to know input and output dirs of apk-inliner,
 which is the reason for evaluation dependence.
 */
evaluationDependsOn(":projects:apk-inliner")
JavaExec inlinerExec = project(":projects:apk-inliner").tasks.run as JavaExec

File apkFixturesDir = new File(
  IterableExtensions.findSingle(sourceSets.test.resources.srcDirs as Set<File>),
  BuildKt.apk_fixtures)
assert apkFixturesDir.directory

task inlineMonitoredApkFixture(type: JavaExec, dependsOn: "buildApkFixtures") {JavaExec je ->
  ["main", "classpath", "standardOutput"].each {delegate."$it" = inlinerExec."$it"}

  File inputApk = new File(apkFixturesDroidmateStagingDir, monitoredApkFixtureName)
  je.args = [
    BuildKt.apk_inliner_param_input, inputApk.canonicalPath,
    BuildKt.apk_inliner_param_output_dir, apkFixturesDir
  ]

  je.inputs.file inputApk
  je.outputs.file new File(apkFixturesDir, BuildKt.monitored_inlined_apk_fixture_name)
}

task pullApkFixtures(type: Copy, dependsOn: "buildApkFixtures") {

  from fileTree(dir: apkFixturesDroidmateStagingDir, include: "*.apk", exclude: monitoredApkFixtureName)
  into apkFixturesDir
}

task buildApkFixtures(type: GradleBuild) {

  buildFile = apkFixturesProjectDir.path + "/build.gradle"
  tasks = [apkFixturesBuildTask]
}

//endregion Preparation of apk fixtures for testing

// Ensure BuildKt.appguard_apis_txt is available at runtime as a resource.
processResources.configure { Copy copy -> copy.from(project(":projects").file("resources")) }

//region Preparation of resources: common code
private File getCoreResDir()
{
  Set<File> coreResDirs = sourceSets.main.resources.srcDirs.findAll {it.path.contains(project.name)}
  assert coreResDirs.size() == 1
  return coreResDirs.find()
}
//endregion

//region cleaning resources

task cleanResources(type: Delete) {
  delete file(new File(coreResDir, uiaDaemonApkName))
  delete file(new File(coreResDir, uiaDaemonTestApkName))
  delete file(new File(coreResDir, monitorApkName))
}

task cleanTest(type: GradleBuild) {

  buildFile = apkFixturesProjectDir.path + "/build.gradle"
  tasks = ["clean"]
}

//endregion

//region Testing tasks

test.configure {

  inputs.dir apkFixturesDir.absolutePath

  /* The tests have to be run from root project dir (droidmate) for the classpaths to be properly resolved.

  API reference:
  workingDir  http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html
  rootDir     http://www.gradle.org/docs/current/dsl/org.gradle.api.Project.html
  */
  workingDir rootDir

   /* Lurking bug: GRADLE-1682. See :projects:command:test doc for more.*/
   includes = ['']
}
//endregion Testing tasks

apply from: project(":projects").file("maven.gradle")
apply from: project(":projects").file("debug.gradle")

// WISH right now logback.groovy is in the built jar, thus making it hard to replace:
// http://stackoverflow.com/a/18275945/986533
// http://stackoverflow.com/a/26452714/986533
//
// More precisely, because it is in resources dir of main sourceSet, it gets deployed to the jar with :jar task. Ideally it
// should not be in the jar at all, nor in the distribution ('application' plugin / 'installDist' task). See the links above
// on the better setup.
