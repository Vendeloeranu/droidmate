// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org
import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'groovy'
apply plugin: 'project-report' // http://www.gradle.org/docs/current/userguide/project_reports_plugin.html

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

/*
 This project uses test artifacts of some other projects, so it has to depend on them. Strictly speaking, these calls are necessary
 for dependencies of type "testCompile project(":projects:core").sourceSets.test.output" to work. Otherwise, "sourceSets" would
 not be recognized.
 WISH this is a hack. Correct solution is described here: http://stackoverflow.com/a/15361666/986533

 https://discuss.gradle.org/t/multiproject-testcompile-dependencies-not-seen-by-dependant-project/6779
 http://stackoverflow.com/questions/5644011/multi-project-test-dependencies-with-gradle
 */
evaluationDependsOn(":projects:core")
evaluationDependsOn(":projects:reporter")
evaluationDependsOn(":projects:lib-kotlin")

// Necessary to make CI server work. Looks like Groovy doesn't maintain necessary ordering with Kotlin. The build doesn't always
// fail without this dependency, but it fails on travis-ci.
compileTestGroovy.dependsOn ":projects:lib-kotlin:build"

/*
  Workaround for a bug causing tests run from Gradle to fail. Source of the workaround: [1]

  Suspected cause of the bug:
    Because project(":projects:monitor-template") has android.jar as a transitive dependency, running tests
    from Gradle caused JUnit to throw "Stub!" from Assert method.

  [1] http://stackoverflow.com/a/7253941/986533
  [2] http://stackoverflow.com/questions/2422378/intellij-idea-with-junit-4-7-junit-version-3-8-or-later-expected
  [3] https://docs.gradle.org/2.5/release-notes#changes-in-ide-classpath-generation
      Linked from: https://youtrack.jetbrains.com/issue/IDEA-125971#comment=27-1087279
  [4] https://youtrack.jetbrains.com/issue/IDEA-148402
      Lined fromhttps://youtrack.jetbrains.com/issue/IDEA-125971#comment=27-1267028
 */
configurations {forceJunitPriority}
sourceSets.test.runtimeClasspath = configurations.forceJunitPriority + sourceSets.test.runtimeClasspath
sourceSets.test.compileClasspath = configurations.forceJunitPriority + sourceSets.test.compileClasspath

apply plugin: 'application'
mainClassName = "org.droidmate.frontend.DroidmateFrontend"

(tasks.run as JavaExec).configure {

  assert sourceSets.main.runtimeClasspath == classpath

  workingDir rootDir
  java.nio.file.Path argsFile = Paths.get("$rootDir/args.txt")
  if (Files.exists(argsFile))
  {
    List<String> appArgs = argsFile.text.readLines().first().split(" ") as List<String>
    args appArgs
  }
}

dependencies {

  forceJunitPriority 'junit:junit:4.12'
  testCompile 'junit:junit:4.12'

  // See the comment to "evaluationDependsOn" for explanation of these dependencies.
  testCompile project(":projects:core").sourceSets.test.output
  testCompile project(":projects:reporter").sourceSets.test.output
  testCompile project(":projects:lib-kotlin").sourceSets.test.output

  // Necessary to make tests from 'lib-kotlin' work.
  testCompile 'org.xmlunit:xmlunit-core:2.1.1'

  // Necessary to make tests from 'reporter' work.
  testCompile 'org.hamcrest:hamcrest-library:1.3'

  // Workaround for: 'gradlew :p:command:test' fails when reporter tests are run from command line.
  // It works when run from IntelliJ or when run with 'gradlew :p:reporter:test'
  testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

  compile project(":projects:apk-inliner")
  compile project(":projects:core")
  compile project(":projects:reporter")
}

apply from: file("test.gradle")

apply from: project(":projects").file("maven.gradle")
// So the published jar to local maven repo with task "install" won't be called command.jar but droidmate.jar
jar.baseName = rootProject.name

apply from: project(":projects").file("debug.gradle")