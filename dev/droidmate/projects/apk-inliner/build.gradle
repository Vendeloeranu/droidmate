// Copyright (c) 2012-2016 Saarland University
// All rights reserved.
//
// Author: Konrad Jamrozik, jamrozik@st.cs.uni-saarland.de
//
// This file is part of the "DroidMate" project.
//
// www.droidmate.org

import org.droidmate.buildsrc.BuildKt

apply plugin: 'groovy'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {

  compile buildSrcJars

  compile project(":projects:lib-common")

  compile 'org.codehaus.groovy:groovy-all:2.4.6'

  testCompile group: 'junit', name: 'junit', version: '4.12'

  Set<File> resourcesDirs = project.sourceSets.main.resources.srcDirs
  assert resourcesDirs.size() == 1
  runtime files(new File(resourcesDirs[0], "logback"));
}

apply plugin: 'application'
mainClassName = "org.droidmate.apk_inliner.ApkInlinerFrontend"

run {

  standardOutput = new ByteArrayOutputStream() // Mute the task stdout

  ext.targetApksDir = file(BuildKt.apk_inliner_param_input_default.toFile())
  ext.inlinedApksDir = file(BuildKt.apk_inliner_param_output_dir_default.toFile())

  if (!targetApksDir.isDirectory())
    targetApksDir.mkdir()

  inputs.dir targetApksDir
  outputs.dir inlinedApksDir
}

clean.configure {
  doLast {
    File inlinedApksDir = run.outputs.files.getSingleFile()
    inlinedApksDir.listFiles().each {if (it.name.endsWith(".apk")) assert it.delete()}
  }
}

test.configure {
  // includes = [''] // Turn off all tests
}